subprojects {
    apply plugin: 'com.android.application'

    android {
        compileOptions.encoding = "UTF-8"
        version = project.APP_VERSION

        compileSdkVersion ANDROID_BUILD_SDK_VERSION
        buildToolsVersion ANDROID_BUILD_TOOL_VERSION

        defaultConfig {
            minSdkVersion ANDROID_BUILD_MIN_SDK_VERSION
            targetSdkVersion ANDROID_BUILD_TARGET_SDK_VERSION
            versionCode Integer.parseInt(project.APP_VERSION)
            versionName project.APP_VERSION_NAME
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                debuggable true
//                minifyEnabled true
                zipAlignEnabled true
//                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        flavorDimensions 'phase'

        productFlavors {
            alpha {
                dimension 'phase'
            }

            sandbox {
                dimension 'phase'
            }

            beta {
                dimension 'phase'
            }

            real {
                dimension 'phase'
            }
        }

        variantFilter { variant ->
            String phase = variant.flavors.find { it.dimension == 'phase'}.name
            if (variant.buildType.name == 'release') {
                setIgnore(true)
            }
            // Do not change this line because zipFullSource task depends on this line.
            if (phase != 'real') { setIgnore(true) }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        lintOptions {
            checkReleaseBuilds true
            abortOnError false
            checkAllWarnings true
            xmlReport true
            htmlReport true
            disable "InvalidPackage", "MissingTranslation"
        }

        packagingOptions {
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
            exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        }
    }

    preBuild.dependsOn rootProject.bumpVersionInProperties

    dependencies {
        androidTestImplementation('com.android.support.test:runner:0.5') {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation('com.android.support.test:rules:0.5') {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        // Optional -- Hamcrest library
        // Optional -- UI testing with Espresso
        androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation('com.android.support.test.espresso:espresso-intents:2.2.2') {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation('com.android.support.test.espresso:espresso-web:2.2.2') {
            exclude group: 'com.android.support', module: 'support-annotations'
        }

        androidTestImplementation "com.android.support:support-annotations:$ANDROID_SUPPORT_LIB_VERSION"
        androidTestImplementation "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    }
}